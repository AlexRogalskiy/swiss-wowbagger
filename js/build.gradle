apply plugin: 'kotlin-platform-js'
apply plugin: 'kotlin2js'

archivesBaseName = 'swiss-wowbagger-js'

repositories {
    mavenCentral()
}

dependencies {
    expectedBy project(':common')
    compile libraries.kotlin_stdlib_js
    testCompile libraries.kotlin_test_js
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin
}

artifacts {
    archives sourcesJar
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web"

    dependsOn classes
}
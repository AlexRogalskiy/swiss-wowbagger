apply plugin: 'kotlin-platform-jvm'
apply plugin: 'signing'
apply plugin: 'maven-publish'

dependencies {
    expectedBy project(':common')
    compile libraries.kotlin_stdlib
    compile libraries.kotlin_stdlib_jre7
    compile "guru.nidi:mbrola-jvm:0.0.1-SNAPSHOT"
    testCompile libraries.junit
    testCompile libraries.kotlin_test
}

// this is needed because intellij does not include build/resources/main in the classpath
task copyResources(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/classes/main"
}
processResources.dependsOn copyResources

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from project(':common').sourceSets.main.kotlin
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'swiss-wowbagger'
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ''
                password = ''
            }
        }
    }
}